<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.12">
  <POU Name="Axis2" Id="{c09083b7-231a-4eec-ae03-21020f911b6e}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM Axis2
VAR
	
	sAxisDescription						:STRING	:= 'Axis 2 - EL7201 - AM8111-0F20';	

(* Axis - basic functions *)
	fbAxis									:FB_Axis;
	fMoveAbsPosition						:LREAL;				(* Target position *)
	fMoveAbsVelocity						:LREAL	:= 500.0;	(* Target speed axis 2 max. 10000.0 Degree/s *)
	bStartMoveAbs							:BOOL;
	bMoveAbsBusy							:BOOL;
	bMoveAbsDone							:BOOL;
	bAxisErr								:BOOL;
	nAxisErrId								:DWORD;
	bReset									:BOOL;
	bStop									:BOOL;
	bStopAxisDone							:BOOL;
	bStopAxisBusy							:BOOL;
	bRevSeqStart							:BOOL;
	bStartparameterSet						:BOOL;
	
(* Test - change target position - Axis *)
	bTestChangeTargetPos					:BOOL;				(* Test - change target position *)
	bMaxPositionReached						:BOOL;

(* Couple Axis on Handwheel) *)
	fbGearIn								:MC_GearIn;
	bGearInDone								:BOOL;
	bAxisCoupled							:BOOL;

(* Uncouple Axis *)
	fbGearOut								:MC_GearOut;
	bGearOutDone							:BOOL;

END_VAR
(* End *)
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Axis 2 *)

(* Axis - basic functions  *)

	IF NOT bStartparameterSet
	THEN
		(* Startup Parameter reversing sequence *)
		fbAxis.fRevSeqTargetPos1	:= 1800.0;
		fbAxis.fRevSeqTargetVelo1	:= 1500.0;
		fbAxis.fRevSeqTargetPos2	:= 0.0;
		fbAxis.fRevSeqTargetVelo2	:= 200.0;
		fbAxis.nRevSeqIdleTime		:= 500;
		bStartparameterSet			:=TRUE;
	END_IF
		
	fbAxis(
		bPowerEnable		:= 	bSwitchS13,
		fPowerOverride		:= 	F_Scaling(nAnalogInR10, 100.0, 32767.0),
		bMoveAbsExecute		:= 	(bStartMoveAbs OR bSwitchS16) AND NOT stAxis2.Status.Coupled,
		fMoveAbsPosition	:= 	fMoveAbsPosition,
		fMoveAbsVelocity	:= 	fMoveAbsVelocity,
		bReset				:= 	bReset OR bSwitchS21,
		bStopAxis			:= 	bStop,
		bRevSeqStart		:=  (bSwitchS20 OR bRevSeqStart) AND fbAxis.bPowerStatus, 
		stAxis				:= 	stAxis2,
		bPowerStatus		=> 	bLedH13,
		bMoveAbsDone		=> 	bMoveAbsDone,
		bMoveAbsBusy		=> 	bMoveAbsBusy,
		bAxisErr			=> 	bAxisErr,
		nAxisErrId			=> 	nAxisErrId,
		bStopAxisDone		=> 	bStopAxisDone,
		bStopAxisBusy		=> 	bStopAxisBusy);

(* Test - change target position - Axis *)
	IF bTestChangeTargetPos
		AND (fMoveAbsPosition <= stAxis2.NcToPlc.ActPos)
			AND NOT bMaxPositionReached
	THEN
		fMoveAbsPosition := fMoveAbsPosition + 180.0;
	ELSE
		IF bTestChangeTargetPos
			AND (fMoveAbsPosition >= 3600.0)
		THEN
			fMoveAbsPosition 	:= 0.0;
			bMaxPositionReached	:= TRUE;
		END_IF
	END_IF

	IF bTestChangeTargetPos
		AND (stAxis2.NcToPlc.ActPos < 1.0)
	THEN
		bMaxPositionReached	:= FALSE;
	END_IF

	IF bSwitchS17
	THEN
		bMaxPositionReached	:= FALSE;
		fMoveAbsPosition 	:= 0.0;
	END_IF
	
(* Couple Axis on Handwheel *)
	fbGearIn(
		Execute			:= 	bHandwheelSwitchS28,
		RatioNumerator	:= 	1,
		RatioDenominator:= 	1,
		Acceleration	:= 	,
		Deceleration	:= 	,
		Jerk			:= 	,
		BufferMode		:= 	,
		Options			:= 	,
		Master			:= 	stAxis5,
		Slave			:= 	stAxis2,
		InGear			=> 	bGearInDone,
		Busy			=> 	,
		Active			=> 	,
		CommandAborted	=> 	,
		Error			=> 	,
		ErrorID			=> 	);

(* Axis coupled *)
	bAxisCoupled := stAxis2.Status.Coupled;

(* Uncouple Axis *)
	fbGearOut(
		Execute	:= 	bSwitchS17,
		Options	:= 	,
		Slave	:= 	stAxis2,
		Done	=> 	bGearOutDone,
		Busy	=> 	,
		Error	=> 	,
		ErrorID	=> 	);
	
(* End *)]]></ST>
    </Implementation>
    <LineIds Name="Axis2">
      <LineId Id="3" Count="91" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>